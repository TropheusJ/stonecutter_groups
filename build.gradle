plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft("com.mojang:minecraft:$minecraft_version")
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$qm_version:intermediary-v2")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })
    }

    processResources {
        Map<String, Object> properties = [
                "version": version,
                "minecraft_version": minecraft_version,

                "fabric_api_version": fabric_api_version,
                "loader_version": fabric_loader_version,

                "forge_version": forge_version
        ]

        properties.forEach(inputs::property)

        filesMatching(["fabric.mod.json", "mods.toml"]) {
            expand properties
        }
    }

    publishing {
        publications {
            register("mavenJava", MavenPublication) {
                artifactId = archivesBaseName + "_${project.name}"
                from(components.java)
            }
        }

        repositories {
            maven {
                url = "https://mvn.devos.one/${System.getenv("PUBLISH_SUFFIX")}/"
                credentials {
                    username = System.getenv("MAVEN_USER")
                    password = System.getenv("MAVEN_PASS")
                }
                authentication {
                    create("basic", BasicAuthentication)
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    archivesBaseName = archives_base_name
    group = maven_group

    String runNumber = System.getenv("GITHUB_RUN_NUMBER")
    String build = runNumber != null ? "build.$runNumber" : "local"
    version = "$mod_version-$build+mc$minecraft_version"

    repositories {
        maven { url = "https://maven.quiltmc.org/repository/release" } // QM
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    tasks.register("buildOrPublish") {
        group = "build"
        String mavenUser = System.getenv("MAVEN_USER")
        if (mavenUser != null && !mavenUser.isEmpty()) {
            dependsOn(tasks.named("publish"))
            println("prepared for publish")
        } else {
            dependsOn(tasks.named("build"))
            println("prepared for build")
        }
    }
}
